\documentclass{scrreprt}

% They are all over the place
\usepackage{todonotes}

% Appendices
\usepackage[header,title,titletoc]{appendix}
\renewcommand{\appendixname}{Appendix}

% License
\usepackage[
    type={CC},
    modifier={by-sa},
    version={3.0},
]{doclicense}

% Add bibliography to TOC
\usepackage[nottoc,numbib]{tocbibind}

% Sane datestamps
\usepackage[yyyymmdd]{datetime}
\renewcommand{\dateseparator}{--}

% Haskell code highlighting
\usepackage{minted}
\newenvironment{code}{\VerbatimEnvironment\begin{minted}{haskell}}{\end{minted}}
\setminted{fontsize=\small,baselinestretch=1}

% Include images
\usepackage{graphics}

% Links and their colors
\usepackage[
  colorlinks=true,
  linkcolor=darkgray,
  citecolor=violet,
  urlcolor=darkgray,
  ]{hyperref}

% Change the geometry of some of the pages 
\usepackage{geometry}

% Put some pages in landscape
\usepackage{rotating}

% No extra margin before chapters
\RedeclareSectionCommand[beforeskip=0pt]{chapter}

\begin{document}

\title{Supporting Competent Authorities in the Implementation of the NIS
Directive for Safety-Critical Industries}
\subtitle{Analysis through attack-defense trees}
\author{Uma Zalakain \\ \href{mailto:2423394z@student.gla.ac.uk}{2423394z@student.gla.ac.uk}}

{\let\newpage\relax\maketitle}
\doclicenseThis
\newpage

\pagenumbering{roman}
\setcounter{tocdepth}{2}
\tableofcontents
\newpage
\pagenumbering{arabic}

\chapter{Introduction}

The EU directive on the security of Networks and Information Systems (NIS) has
been integrated into UK law. This directive aims to improve the reliability and
security of those network and information systems that are essential to the
economy, society and individual's
welfare.\footnote{\url{https://www.ncsc.gov.uk/guidance/introduction-nis-directive}}

The NIS directive does not require Operators of Essential Services (OES) to
conform to a particular set of fixed rules. Instead, it hands the responsibility
of oversight to the relevant Competent Authority (CA). The NIS directive
postulates 14 general
principles\footnote{\url{https://www.ncsc.gov.uk/guidance/table-view-principles-and-related-guidance}}
-- CAs are strongly
encouraged\footnote{\url{https://www.gov.uk/government/publications/nis-regulations-guidance-for-competent-authorities}}
to use the Cyber Assessment Framework
(CAF)\footnote{\url{https://www.ncsc.gov.uk/guidance/nis-directive-cyber-assessment-framework}}
to evaluate whether OES follow them. Depending on the sector, a CA may issue
more specific guidance. One such example is the guidance for Cyber Security for
Industrial Automation and Control
Systems\footnote{\url{http://www.hse.gov.uk/foi/internalops/og/og-0086.pdf}}
issued by the Health and Safety Executive.

\section{Contributions}

\S\ref{tool} presents \textit{ADTrees}, a Haskell library capable of
\textbf{modelling}, \textbf{rendering}, finding \textbf{minimal cutsets} and
performing \textbf{quantitative analysis} on attack defense trees. Some
familiarity with Haskell is required to fully understand how the proposed
solution is built; providing an introduction to Haskell is out of the scope of
this project.

\S\ref{evaluation} demonstrates how a CA might use this tool to guide an asses
an OES by performing a case analysis on the provision of the top-level domain
\texttt{.uk} by the provider Nominet\footnote{\url{https://nominet.uk}}. As the
designated regulator for digital infrastructure, Ofcom is the CA for Nominet. In
its guidance pertaining NIS, Ofcom asks OES to assess themselves against the
CAF\footnote{\url{https://www.ofcom.org.uk/phones-telecoms-and-internet/information-for-industry/guidance-network-information-systems-regulations}}.

\S\ref{results} points out which of the points laid out in the CAF has the usage
of ADTrees tool contributed to. \todo{}

\S\ref{conclusions} end with \todo{}.

\todo{document code}

\chapter{A Haskell library for attack defense trees}
\label{tool}

\section{Attack defense trees}

Fault trees were first introduced as a graphical and logical tool for modelling
the way in which basic events interact to lead to an undesired behaviour
\cite[IV.1]{Vesely1981}. They are therefore useful in safety-critical systems.
They consist of nodes and leafs: each node is a logical gate; each leaf a basic
event. This structure makes fault trees attractive for quantitative evaluation.
It is important to note that all basic events in a fault tree \textit{must} be
unique and independent. If not the case, every directed acyclic graph
representing a fault model can be rewritten as a fault tree.  \todo{citation
needed}

Attack trees take the idea of fault trees and adapt it to model attack scenarios
in security-critical systems \cite{Schneier1999} \cite{Brooke2003}
\cite{NaiFovino2009}\todo{get rid of some citations}: an attacker's objective
(at the root of the tree) is repeatedly refined into subgoals connected by
logical gates. Yet, this model is unable to represent any defensive measure.

Attack defense trees (ADTs) extend attack trees by introducing nodes that
represent defensive measures \cite{KordyFoundations}. Every node is thus an
action of either the attacker or the defender: interactive scenarios can be
represented.  As with fault trees, refinements can either be disjunctive or
conjunctive. To allow a quantitative evaluation of ADTs, nodes have to be
independent of each other -- alternative proposals relying on DAGs exist, but
are more complex \cite{KordyDAG}.

\todo{More on the technique itself?}

% TODO: relate to NIS: CAF points
%   IMPORTANT: How attack defense trees help

\section{ADTrees}

\textit{ADTrees}\footnote{\url{https://github.com/umazalakain/adtrees}}
is a small Haskell library for attack defense trees. It is designed with the
following goals in mind:

\todo{this feels too short}
\begin{itemize}
    \item To provide a \textbf{small and simple} interface for modelling attack
        defense trees while remaining \textbf{extendable}. \todo{mention no
        parser}
    \item To provide a \textbf{flexible} interface to \textbf{quantitative
        analysis} while providing some commonly used algebras.
    \item To \textbf{aid decision making} by generating minimal cutsets of the
        tree.
    \item To \textbf{visualise} any modelled tree.
\end{itemize}

ADTrees is inspired by ADTool \footnote{\url{https://github.com/tahti/ADTool2}},
a graphical interface developed in Java for creating and visualising attack
defense trees \cite{KordyTool}. While ADTool is a graphical tool, ADTrees is a
library: this contributes to keeping any superfluous functionality at minimum.
ADTrees also further simplifies the structure of countermeasure nodes. Both the
structure and the algebra of attack defense trees can be expressed cleanly
through Haskell's union types.

\subsection{Tree modelling}

\begin{figure}[h!]
    \label{example-adt}
    \centering
    \includegraphics[width=\textwidth]{bankaccount}
    \caption{Example attack defense tree generated by ADTrees. Red nodes
    represent the attacker's actions, green nodes the defendant's. Triangles
    facing down represent OR gates, triangles facing up AND gates, diamonds
    COUNTER measures, and boxes BASIC events.  Basic events contain multiple
    attributes for quantitative analysis.}
\end{figure}

Trees are represented by an inductive datatype. This datatype is generalised
over \texttt{a}, which may contain information of arbitrary type that is later
used for quantitative analysis. The leafs of the tree, represented by the
constructor \mintinline{Haskell}{Basic} contain this information. Constructors
\mintinline{Haskell}{And} and \mintinline{Haskell}{Or} represent logical gates
containing a list of subtrees. The constructor \mintinline{Haskell}{Counter}
represents two opposing nodes: its left subtree contains actions by the same
player; its right subtree contains actions by its opponent.

\begin{code}
data ADTree a 
    = Basic   Name a
    | And     Name [ADTree a]
    | Or      Name [ADTree a]
    | Counter Name (ADTree a) (ADTree a)
\end{code}

The example ADT in Figure \ref{example-adt} carries information about the
probability and difficulty of each basic event. Such a tree can be represented
by the type \mintinline{Haskell}{ADTree (Fractional, Fractional)} and
encoded as follows:

\label{example-model}
\begin{code}
Or "Bank Account" [
    And "ATM" [
        Or "PIN" [
            Basic "Eavesdrop" (0.01, 0.3),
            Counter "Get Note"
                (Basic "Find Note" (0.40, 0.3))
                (Basic "Memorize" (0.80, 0.1)),
            Basic "Force" (0.01, 0.9)],
        Basic "Card" (0.1, 0.7)],
    Counter "Online"
        (And "Login" [
            Or "Password" [
                Basic "Phishing" (0.90, 0.4),
                Basic "Key logger" (0.20, 0.6)],
            Basic "User name" (0.90, 0.1)])
        (Counter "2nd Auth Factor"
            (Or "Usage" [
                Basic "Key Fobs" (0.01, 0.1),
                Basic "PIN Pad" (0.01, 0.3)])
            (Or "Malware" [
                Basic "Browser" (0.20, 0.5),
                Basic "OS" (0.20, 0.4)]))]
\end{code}

\subsection{Quantitative analysis}

To perform quantitative analysis over customisable basic event attributes, the
user must be able to specify an algebra on them. There exist three cases which
this algebra has to take into consideration: OR gates, AND gates, and COUNTER
measures. Several such algebras exist \cite{KordyQuantitative}. ADTrees exposes
a simplified interface for defining them:

\begin{code}
data PSemantics a = MkPSemantics
    { plus    :: a -> a -> a -- OR operator
    , zero    :: a           -- OR default
    , times   :: a -> a -> a -- AND operator
    , one     :: a           -- AND default
    , counter :: a -> a -> a -- COUNTER measure
    }
\end{code}

Algebras might need to change according to the player. For example, it might be
desirable to computer the cost just for the attacker's actions. This is
accommodated by making the algebra depend on the \mintinline{Haskell}{Player}:

\begin{code}
type Semantics a = Player -> PSemantics a
\end{code}

In the following example probability is defined -- which does not depend on who
the current player is, thus that argument is ignored:

\begin{code}
probability :: Semantics Rational
probability _ = MkPSemantics
    { plus    = \x y -> x + y - x * y
    , zero    = 0
    , times   = (*)
    , one     = 1
    , counter = (-)
    }
\end{code}

On the other hand, the difficulty for the attacker depends on the player of each
node:

\begin{code}
difficulty :: Semantics Rational
difficulty A = MkPSemantics
    { plus    = min
    , zero    = 1
    , times   = max
    , one     = 0
    , counter = max
    }
difficulty D = MkPSemantics
    { plus    = max
    , zero    = 0
    , times   = min
    , one     = 1
    , counter = min
    }
\end{code}

Other common algebras for quantitative analysis can be found in Appendix
\ref{algebras}.

Once a semantics is defined, an attack defense tree can be inductively
evaluated:

\begin{code}
evaluate :: Semantics a -> Player -> ADTree a -> a
evaluate _   _ (Basic _ a)
    = a
evaluate sem p (Or _ cs)
    = foldr (plus (sem p) . evaluate sem p) (zero $ sem p) cs
evaluate sem p (And _ cs)
    = foldr (times (sem p) . evaluate sem p) (one $ sem p) cs
evaluate sem p (Counter _ a d)
    = counter (sem p) (evaluate sem p a) (evaluate sem (switchPlayer p) d)
\end{code}

As shown in \ref{example-model}, attack defense trees may contain multiple
attributes. To facilitate the reuse of algebras, a tree with multiple attributes
can be translated into a tree with a single attribute, to which an already
defined algebra can be applied. This translation is possible because
\mintinline{Haskell}{ADTree a} follows the functor laws.\todo{}

In the following example, \mintinline{Haskell}{fmap} applies a function to the
attributes of all \mintinline{Haskell}{Basic} events of a tree, while
\mintinline{Haskell}{snd} selects the second element of a tuple. Put together, a
tree with two attributes is translated into a tree with just the second of those
attributes:

\begin{code}
> :t example
    example :: ADTree (Rational, Rational)
> :t fmap snd example
    fmap snd example :: ADTree Rational
\end{code}

The user can then use the evaluation function as follows:

\begin{code}
> evaluate difficulty A (fmap snd example)
2 % 5
\end{code}

\subsection{Minimal cutsets}

In standard fault trees, cutsets are sets of events that cause the top-level
fault to occur. Minimal cutsets are those cutsets that do not contain any other
cutsets as a subset.

Attack defense trees introduce countermeasures nodes containing opposing
subnodes. I extend the concept of minimal cutsets to include countermeasure
nodes, for which the combinatory product of the attacker's and the defender's
cutsets are given. The resulting cutsets represent all possible attack-defense
interaction which can lead to the root goal to be achieved.

\begin{code}
cutsets :: ADTree a -> [ADTree a]

cutsets (Basic n a) = [Basic n a]

-- One cutset from every children
-- Cartesian product of all combinations
cutsets (And n es) = map (And n) (mapM cutsets es)

-- Any cutset from any children
cutsets (Or n es) = map (Or n . (: [])) (concatMap cutsets es)

-- One cutset from the attacker, one from the deffender
-- Cartesian product of all combinations
cutsets (Counter n a d) = Counter n <$> cutsets a <*> cutsets d
\end{code}
%stopzone

These cutsets can then be used for decision making purposes. The following
snippet generates all possible cutsets for the tree represented in Figure
\ref{example-adt}, orders them ascendantly according to probability, and yields
back the most probable cutset. The result has been indented for clarity.

\begin{code}
> last (sortOn (evaluate probability A . fmap fst) (cutsets example))
Or "Bank Account" [
    Counter "Online"
        (And "Login" [
            Or "Password" [
                Basic "Phishing" (9 % 10,2 % 5)],
            Basic "User name" (9 % 10,1 % 10)])
        (Counter "2nd Auth Factor"
            (Or "Usage" [
                Basic "PIN Pad" (1 % 100,3 % 10)])
            (Or "Malware" [
                Basic "OS" (1 % 5,2 % 5)]))]
\end{code}

Similar techniques can be used to aid decision making. As an example, the cost
of fighting script-kiddies and other unskilled attackers can be computed by
filtering cutsets beneath a certain level of attacker-skill, joining those
cutsets back into a common tree, then computing its cost for the defender.

\subsection{Rendering}

ADTrees uses Graphviz\footnote{\url{https://graphviz.org}} for tree rendering.
ADTrees automatically translates a given tree into its Graphviz representation.
Then Graphviz can be used to transform this declarative description of a graph
into an image. The tree in Figure \ref{example-adt} is an example of this.
This idea was inspired by \textit{fault-tree}, a small Haskell library for fault
trees\footnote{\url{https://github.com/tomahawkins/fault-tree}}. Unfortunately
Graphviz does not include node shapes for logic gates, and its support for
custom node shapes is very limited: I chose to use triangles facing up to
represent AND gates, triangles facing down to represent OR gates.

Assuming equality on attribute \texttt{a} is decidable, given a function that
translates any \texttt{a} into a string, and the player of the root node,
\mintinline{Haskell}{dot} will generate a Graphviz representation of an
\mintinline{Haskell}{ADTree a}:

\begin{code}
dot :: (Eq a) => (a -> String) -> Player -> ADTree a -> String
\end{code}

\chapter{Evaluation}
\label{evaluation}

This section uses ADTrees to model the security issues involved in providing an
authoritative DNS service for a top-level domain like \texttt{.uk}. It limits
itself to analysing those weaknesses and defensive measures pertaining the
provision of DNS services in itself, ignoring attacks that may take place
further down the line -- these are extensively documented in
\cite{weaknesses-dns}, \cite{rfc3833} and \cite{dns-bind-sec}.

Both threats and protective measures are extracted from \cite[sections 3, 5, 7,
and 10]{secdns} and \cite{dns-threat-analysis}. Some are omitted, as they are
not pertinent to the provisioning of a top-level domain. 

Unfortunately, uniform statistical measurements concerning these threads and
defensive measures is close to nonexistent. To demonstrate how ADTrees can be
used to communicate and make decisions, I annotate the basic events in the tree
with fabricated values -- these values are semi-arbitrary and reflect my
personal judgment. For the definitions of the algebras on both attributes refer
to Appendix \ref{algebras}.
\todo{Add severity to the annex of algebras}

\begin{description}
    \item [Difficulty] involved in performing an action. Takes into
        consideration the physical infrastructure, cost and time required.
    \item [Severity] of the damage caused by an action. The severity on the
        defender's nodes indicates how alleviating their effects are. Accounts
        for the failure mode, time and cost involved in recovering,
        detectability, and reach of the disruption.
\end{description}

The resulting ADT is defined in Appendix \ref{dns-def} and can be seen in Figure
\ref{dns-tree}.

\todo{what kind of computation do we make}

\newgeometry{top=0.5cm,bottom=0.5cm}
\begin{sidewaysfigure}[h]
    \label{dns-tree}
    \centering
    \includegraphics[width=1.3\paperwidth]{dns}
    \caption{}
\end{sidewaysfigure}
\restoregeometry

\chapter{Results}
\label{results}

\todo{mention LOC}
Concretely, the library eases the evaluation of the following points of the
Cyber Assessment Framework:

\todo{revise these are met in evaluation}
\begin{description}
    \item [A2a]
        \begin{itemize}
            \item Your organisational process ensures that security risks to
                networks and information systems relevant to essential services
                are identified, analysed, prioritised, and managed.
            \item Your approach to risk is focused on the possibility of
                disruption to your essential service, leading to a detailed
                understanding of how such disruption might arise as a
                consequence of possible attacker actions and the security
                properties of your networks and information systems.
        \end{itemize}

    \item [B1a]
        \begin{itemize}
            \item Your systems are designed so that they remain secure even when
                user security policies and processes are not always followed.
        \end{itemize}
\end{description}


\chapter{Conclusions}
\label{conclusions}

\bibliographystyle{apalike}
\bibliography{bibliography}

\newpage
\begin{appendices}
% Only display section titles in the TOC
\addtocontents{toc}{\protect\setcounter{tocdepth}{1}} 

\chapter{Source code}
    \todo{Where to obtain it}
    \todo{How to install it}

\chapter{Authoritative DNS services: an ADT}
\label{dns-def}

\begin{code}
Or "DNS"
    [ Or "data corruption"
        [ Or "repository corruption"
            [ Counter "outdated information"
                ( Basic "(D)DoS on hidden master" (0.8, 0.8) )
                ( Or ""
                    [ Basic "out-of-band replication" (0.2, 0.8)
                    , Basic "tuning of SOA expiration parameters" (0.2, 0.3)
                    ]
                )
            , Or "modified information"
                [ Counter ""
                    ( Basic "master compromised" (0.9, 1.0) )
                    ( Basic "harden master" (0.8, 0.6) )
                , Counter ""
                    ( Basic "secondary compromised" (0.8, 0.8) )
                    ( Basic "establish SLA and OLA with secondary operator" (0.7, 0.5) )
                , Counter ""
                    ( Basic "social engineering" (0.7, 0.7) )
                    ( Basic "secure procedures and education" (0.9, 0.5) )
                ]
            , Or "domain name hijacking"
                [ Basic "typosquatting" (0.2, 0.1)
                , Basic "IDN abuse" (0.2, 0.1)
                ]
            ]
        ]
    , Or "privacy"
        [ Basic "cache snooping" (0.4, 0.3)
        , Basic "NSEC walk" (0.3, 0.3)
        ]
    , Or "denial of service"
        [ Counter "system/application crash"
            ( Basic "specially crafted packet" (0.5, 0.6) )
            ( Basic "diversity OS and DNS server" (0.2, 0.5) )
        , Counter "resource starvation"
            ( Basic "(D)DoS attack" (0.6, 0.7) )
            ( Or ""
                [ Basic "system and network overprovisioning" (0.7, 0.6)
                , Basic "deploy anycast" (0.6, 0.8)
                ]
            )
        ]
    ]
\end{code}

\chapter{Algebras}
\label{algebras}

Following is a list of interesting predefined algebras suitable for quantitative
analysis:

\begin{code}
{-
   Probability of success, assuming all actions are mutually independent
-}
probability :: Semantics Rational
probability _ = MkPSemantics
    { plus    = \x y -> x + y - x * y
    , zero    = 0
    , times   = (*)
    , one     = 1
    , counter = (-)
    }

{-
   Difficulty for the attacker, assuming all attacker's actions are in place
-}
difficulty :: Semantics Rational
difficulty A = MkPSemantics
    { plus    = min
    , zero    = 1
    , times   = max
    , one     = 0
    , counter = max
    }
difficulty D = MkPSemantics
    { plus    = max
    , zero    = 0
    , times   = min
    , one     = 1
    , counter = min
    }

{-
   Minimal cost for the attacker, assuming that all attacker's actions are in
   place and that resources are not reused.
-}
cost :: Semantics Int
cost A = MkPSemantics
    { plus    = min
    , zero    = maxBound
    , times   = (+)
    , one     = 0
    , counter = (+)
    }
cost D = MkPSemantics
    { plus    = (+)
    , zero    = 0
    , times   = min
    , one     = maxBound
    , counter = min
    }

{-
   Minimal skill needed for the attacker, assuming that all attacker's actions
   are in place.
-}
skill :: Semantics Int
skill A = MkPSemantics
    { plus    = min
    , zero    = maxBound
    , times   = max
    , one     = minBound
    , counter = max
    }
skill D = MkPSemantics
    { plus    = max
    , zero    = minBound
    , times   = min
    , one     = minBound
    , counter = min
    }

{-
   Minimal time needed for the attacker, assuming that all attacker's actions
   are in place and that actions are executed in parallel.
-}
timeParallel :: Semantics Int
timeParallel A = MkPSemantics
    { plus    = min
    , zero    = maxBound
    , times   = max
    , one     = minBound
    , counter = max
    }
timeParallel D = MkPSemantics
    { plus    = max
    , zero    = minBound
    , times   = min
    , one     = minBound
    , counter = min
    }

{-
   Minimal time needed for the attacker, assuming that all attacker's actions
   are in place and that actions are executed in sequence.
-}
timeSequence :: Semantics Int
timeSequence A = MkPSemantics
    { plus    = min
    , zero    = maxBound
    , times   = (+)
    , one     = 0
    , counter = (+)
    }
timeSequence D = MkPSemantics
    { plus    = (+)
    , zero    = 0
    , times   = min
    , one     = minBound
    , counter = min
    }

{-
    Satisfiability of the scenario.
-}
satisfiability :: Semantics Bool
satisfiability _ = MkPSemantics
    { plus    = (||)
    , zero    = False
    , times   = (&&)
    , one     = True
    , counter = \x y -> x && not y
    }
\end{code}
\end{appendices}

\end{document}
